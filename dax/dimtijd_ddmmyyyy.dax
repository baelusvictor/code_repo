dim date = 
VAR startdatum =
    MINX (
        {
            --MIN ( 'fct budget'[Datekey] ), -- change this if ever there would be another fact table
            MIN ( 'fct lineitems'[RefDate] )
        },
        [Value]
    )
VAR einddatum =
    MAXX (
        {
            --MAX ( 'fct budget'[Datekey] ), -- change this if ever there would be another fact table
            MAX ( 'fct lineitems'[RefDate] )
        },
        [Value]
    )
    --TODAY()+1200 --"31/12/2026"
RETURN
    GENERATE (
        CALENDAR (
            DATE ( YEAR ( startdatum ), 1, 1 ),
            DATE ( YEAR ( einddatum ), 12, 31 )
        ),
        ROW (
            "datekey", YEAR ( [Date] ) * 10000 + MONTH ( [Date] ) * 100 + DAY ( [Date] ),
            "year", YEAR ( [Date] ),
            "quarter", QUARTER ( [Date] ),
            "month", MONTH ( [Date] ),
            "week", WEEKNUM ( [Date] ),
            "day", DAY ( [Date] ),
            "mmm", FORMAT ( [Date], "mmm" ),
            "YYYYMM", FORMAT ( [Date], "YYYY-MM" ),
            "YYYYQ", FORMAT ( [Date], "YYYY-Q" ),
            "EOM", EOMONTH ( [Date], 0 ),
            "today?",
                IF (
                    FORMAT ( [Date], "dd/mm/yyyy" ) = FORMAT ( TODAY (), "dd/mm/yyyy" ),
                    "today",
                    "not today"
                ),
            "dayindex", DATEDIFF ( [Date], TODAY (), DAY ) * -1,
            "monthindex",
                INT (
                    ( YEAR ( [Date] ) - YEAR ( TODAY () ) ) * 12
                        + MONTH ( [Date] )
                        - MONTH ( TODAY () )
                ),
            "yearindex", INT ( ( YEAR ( [Date] ) - YEAR ( TODAY () ) ) )
        )
    )